# NONE if user wants to get the value
# SOME (signed hash of the string, string)
parameter (option (pair signature string));
# The data
storage (pair key string);
code { DUP; DUP; CAR;
       IF_NONE { DROP; PUSH mutez 1000000; # Fee pattern from July 26
                 AMOUNT; CMPLE; IF {FAIL} {};
                 # Provide the data
                 CDR }
               { DUP; DIP{SWAP}; SWAP; CDAR; # Move key to the top
                 DIP {DUP; CAR; DIP {CDR; H}}; # Arrange the new piece of data
                 CHECK_SIGNATURE;                    # Check to ensure the data is authentic
                 # Update data
                 IF {CDR; SWAP; CDAR; PAIR}
                    # Revert the update. This could be replaced with FAIL
                    {DROP; CDR };
               };
       NIL operation; PAIR}
